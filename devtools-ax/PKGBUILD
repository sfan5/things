pkgname=devtools-ax
_commit=fcaf3ecec0f835cc480fdeb653d33754b0de7624
pkgver=20220801
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers (customized)'
arch=('any')
license=('GPL')
url='https://gitlab.archlinux.org/archlinux/devtools'
depends=('bash' 'openssh' 'rsync' 'arch-install-scripts'
         'git' 'diffutils' 'util-linux' 'awk')
makedepends=('asciidoc')
optdepends=('btrfs-progs: btrfs support')
conflicts=('devtools')
source=(
  "git+https://gitlab.archlinux.org/archlinux/devtools.git#commit=${_commit}"
  0001-archbuild-disable-namcap-by-default.patch
  0002-arch-nspawn-Enable-suppress-sync-yes.patch
  0003-Makefile-get-rid-of-svn-related-tools.patch
  0004-config-add-aarch64-build-configs.patch
  0005-mkarchroot-remove-de_DE-locale.patch
  0006-arch-nspawn-deal-with-differing-mirror-dir-naming-co.patch
  0007-configs-add-aarch64-cross-compile-build-configs.patch
)
sha256sums=('SKIP'
            'da00b5bb9ee1bc6fd8f1bb684f6a813208773dc3238d2c0eddf1cc2380e2d623'
            'cfe321d753f4c7a1a083b9778deb41700091b03a327a724797e9b006e5c8a7c8'
            'd63c2a0542fac92cf0a6ce355d92bdd4d1c12f929e74602da1f8dbabe78135f7'
            '1fbc22ef1db5a2185d455f1c1960d237bd451d0d9b6683a568cd89ddabd177fe'
            '12b4de25d1d2c60e94bfc3169cc6d5e1577915b7cac6aab6c4b275ce17797c7c'
            'b0113c51b426ce84e43bc206aeaa45c96e7907ae76bc83b526972dedbde8258a'
            '0e2f46be67acac668856c887064e99f6d6a5af4e98050c46973318e2db320292')

prepare() {
  cd devtools
  for src in "${source[@]}"; do
    [[ "$src" == *.patch ]] && patch -p1 -i "../$src"
  done
}

build() {
  cd devtools
  make BUILDTOOLVER="${pkgver}-${pkgrel}-${arch}" PREFIX=/usr
}

package() {
  cd devtools
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
