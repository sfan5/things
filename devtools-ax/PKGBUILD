pkgname=devtools-ax
_commit=a510331698bb13ca11ccd7db3f9beef75f87744c
pkgver=20221012
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers (customized)'
arch=('any')
license=('GPL')
url='https://gitlab.archlinux.org/archlinux/devtools'
depends=('bash' 'openssh' 'rsync' 'arch-install-scripts'
         'git' 'diffutils' 'util-linux' 'awk')
makedepends=('asciidoc')
optdepends=('btrfs-progs: btrfs support')
conflicts=('devtools')
source=(
  "git+https://gitlab.archlinux.org/archlinux/devtools.git#commit=${_commit}"
  0001-archbuild-disable-namcap-by-default.patch
  0002-arch-nspawn-Enable-suppress-sync-yes.patch
  0003-Makefile-get-rid-of-svn-related-tools.patch
  0004-config-add-aarch64-build-configs.patch
  0005-mkarchroot-remove-de_DE-locale.patch
  0006-arch-nspawn-deal-with-differing-mirror-dir-naming-co.patch
  0007-configs-add-aarch64-cross-compile-build-configs.patch
  0008-arch-nspawn-set-console-mode-to-autopipe.patch
)
sha256sums=('SKIP'
            'f0ed3739d75000d0ae58431da9f41a7df1d1a77e503fc77ecf3a19fc6e77bca1'
            '9016990e4e41bc4b8f45efe90605e4e8a4765982f31252172ee2357b449adb69'
            '83acd57425bb01c123f3df37eb8e3416dc1a1bd4dcaf4dfdac7661870cbf04a9'
            '56bb4e723399731a48eab64ba4bdfb88b7d4261a6fe91381d5d88c5389ca5784'
            '75c058ac3f3a50639e04f4fbc579fd83bb0919ae8f005c576855237b6a8fb08e'
            '7e8c60f2372f769e50ec14110df27da03b935b63843d27c28c7ac8425aaed358'
            'ee1e2949470fa38d6f4d3865bd6f506bb82d11ee6b671bcac9839565a19e035c'
            '7a7af787fd56d4eb89642ffc5a57251f76ad66c91cd6fc7d953bb4a34feb057d')

prepare() {
  cd devtools
  for src in "${source[@]}"; do
    [[ "$src" == *.patch ]] && patch -p1 -i "../$src"
  done
}

build() {
  cd devtools
  make BUILDTOOLVER="${pkgver}-${pkgrel}-${arch}" PREFIX=/usr
}

package() {
  cd devtools
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
