pkgname=devtools-ax
_commit=6dd7be3fd4d3104101f5a8bbf0f12fcfe8124fd7
pkgver=20230307
pkgrel=1
pkgdesc='Tools for Arch Linux package maintainers (customized)'
arch=('any')
license=('GPL')
url='https://gitlab.archlinux.org/archlinux/devtools'
depends=('bash' 'openssh' 'rsync' 'arch-install-scripts'
         'git' 'diffutils' 'util-linux' 'awk')
makedepends=('asciidoc')
optdepends=('btrfs-progs: btrfs support')
conflicts=('devtools')
source=(
  "git+https://gitlab.archlinux.org/archlinux/devtools.git#commit=${_commit}"
  0001-archbuild-disable-namcap-by-default.patch
  0002-arch-nspawn-Enable-suppress-sync-yes.patch
  0003-Makefile-get-rid-of-svn-related-tools.patch
  0004-config-add-aarch64-build-configs.patch
  0005-mkarchroot-remove-de_DE-locale.patch
  0006-arch-nspawn-deal-with-differing-mirror-dir-naming-co.patch
  0007-configs-add-aarch64-cross-compile-build-configs.patch
  0008-arch-nspawn-set-console-mode-to-autopipe.patch
)
sha256sums=('SKIP'
            'cba36e94d8f1e624fed83c5d047b143d8f4058c1740f3cc218b74fea42f9a55c'
            '0090045176da7d7f22153bb59cb2abf3572493b4ed77e922fc18ffffdb1d9958'
            '9b97fc08e5f318d50a4122e6131cd0de9217877e12c41a9e25f93042fbaa065c'
            '803f5bd2fb540f4e2e36d6f01bafc2461063025168b7e448a6cd9e9ad397f6f8'
            '903e39164e9bc004da9eafd79c51025a7d5c88a8c7735a2f53ca35fce1b30300'
            '5344924d346bb149057708363a5d927256895260621e3ff0bc957eafb442151f'
            'bf818ef03759304d7ec8a7db1134fdfddba07d232c07c604acf43c53a39fba28'
            '01457b9ad91b020c1462a8de99737101c3a6f8e496fcce048c8d3c5f1f54e4c5')

prepare() {
  cd devtools
  for src in "${source[@]}"; do
    [[ "$src" == *.patch ]] && patch -p1 -i "../$src"
  done
}

build() {
  cd devtools
  make BUILDTOOLVER="${pkgver}-${pkgrel}-${arch}" PREFIX=/usr
}

package() {
  cd devtools
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
