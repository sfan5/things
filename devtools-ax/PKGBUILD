pkgname=devtools-ax
_commit=fcaf3ecec0f835cc480fdeb653d33754b0de7624
pkgver=20220801
pkgrel=2
pkgdesc='Tools for Arch Linux package maintainers (customized)'
arch=('any')
license=('GPL')
url='https://gitlab.archlinux.org/archlinux/devtools'
depends=('bash' 'openssh' 'rsync' 'arch-install-scripts'
         'git' 'diffutils' 'util-linux' 'awk')
makedepends=('asciidoc')
optdepends=('btrfs-progs: btrfs support')
conflicts=('devtools')
source=(
  "git+https://gitlab.archlinux.org/archlinux/devtools.git#commit=${_commit}"
  0001-archbuild-disable-namcap-by-default.patch
  0002-arch-nspawn-Enable-suppress-sync-yes.patch
  0003-Makefile-get-rid-of-svn-related-tools.patch
  0004-config-add-aarch64-build-configs.patch
  0005-mkarchroot-remove-de_DE-locale.patch
  0006-arch-nspawn-deal-with-differing-mirror-dir-naming-co.patch
  0007-configs-add-aarch64-cross-compile-build-configs.patch
  0008-arch-nspawn-set-console-mode-to-autopipe.patch
)
sha256sums=('SKIP'
            'bb364a4edfbecfb99706bb71e31006012858a8a7a669acf21bc564477a1b7f80'
            '7d2acf41812ca2f0c28dd0d9bf4574f27a0c957a8199b3ca6e8da05491e6f411'
            'c7db735ae90dbc232b13dfc97e861b5a994b3964f0865298c48afa7722a24e67'
            '5172e06fa9fbf02b73d6eb0796aab88129ca8228b8b79552cf921883a218184c'
            '79b86fd8ebe86d4a682f6cc37c04ba723545894d590b8cba1163ff9dd8755e06'
            '8b36160ad668dc20b1b86259d199fc568351e0f434090858c590c6a53db945b7'
            '0e6a49d684702e27306d845d4cd4db0005aa25cfc5c6501c89011aa637fa1cbd'
            '8aee7a6d03935c140c063cc8c4e33a038e22a71a5d268ccb512b0637c27ea631')

prepare() {
  cd devtools
  for src in "${source[@]}"; do
    [[ "$src" == *.patch ]] && patch -p1 -i "../$src"
  done
}

build() {
  cd devtools
  make BUILDTOOLVER="${pkgver}-${pkgrel}-${arch}" PREFIX=/usr
}

package() {
  cd devtools
  make PREFIX=/usr DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
