# U-Boot: ODROID-C4
# Maintainer: sfan5 <sfan5@live.de>

# Note: must be built on x86

buildarch=8
noautobuild=1

pkgname=uboot-odroid-c4
pkgver=2015.01
pkgrel=3
pkgdesc="U-Boot for ODROID-C4"
arch=('aarch64')
url="https://github.com/hardkernel/u-boot"
license=('GPL')
install=$pkgname.install
makedepends=('bc' 'lib32-gcc-libs' 'lib32-glibc' 'lib32-zlib')
backup=('boot/boot.ini')
_commit=154ddfc826e72c902232169b676b2a4e59c75171 # odroidg12-v2015.01 branch
_toolchain1=gcc-linaro-aarch64-none-elf-4.8-2013.11_linux
_toolchain2=gcc-linaro-arm-none-eabi-4.8-2014.04_linux
source=("https://github.com/hardkernel/u-boot/archive/${_commit}.tar.gz"
        "https://releases.linaro.org/archive/13.11/components/toolchain/binaries/${_toolchain1}.tar.xz"
        "https://releases.linaro.org/archive/14.04/components/toolchain/binaries/${_toolchain2}.tar.xz"
        'boot.ini'
        '0001-sd_fusing-tweaks.diff'
        '0002-arch-linux-arm-modifications.diff'
        '0003-fix-include-issues.diff')
sha1sums=('eb2fc9aa4890cbea5357ab7acf02075f4e205b84'
          '2b3b36d10b72f018c476f09457d0d81fd4baaaeb'
          '8069f484cfd5a7ea02d5bb74b56ae6c99e478d13'
          'c2010dcfceb32a636768cc8cf34357e6a9578f52'
          '0a9aac03daaa4ac1929d6b4ef33569ab93eb7804'
          '43f3e1f5ba825d16a51fe66e2169146d9b421f4d'
          '90ecf9a15c3e900f78ac9fb294d363e2987b8138')

prepare() {
  cd u-boot-${_commit}

  patch -p0 --verbose <../0001-sd_fusing-tweaks.diff
  patch -p0 --verbose <../0002-arch-linux-arm-modifications.diff
  patch -p0 --verbose <../0003-fix-include-issues.diff
}

build() {
  cd u-boot-${_commit}

  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS
  export PATH="${srcdir}/${_toolchain1}/bin:${srcdir}/${_toolchain2}/bin:$PATH"

  make odroidc4_config
  make EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${_commit}/sd_fuse

  install -d "${pkgdir}"/boot
  install -t "${pkgdir}"/boot sd_fusing.sh
  install -t "${pkgdir}"/boot -m 0644 u-boot.bin "${srcdir}"/boot.ini
}
